#!/usr/bin/perl

use strict;
use warnings;
use Date::Parse;
use POSIX qw(strftime);
use Term::ReadKey;
use MIME::Words qw(decode_mimewords); # needs MIME-tools

my $now = strftime "%s", localtime;

my @maildirs = @ARGV;
my ($width) = GetTerminalSize();
unless(@maildirs) {
    open BOXES, "<$ENV{HOME}/.mailboxes" or 
        die "Could not open '$ENV{HOME}/.mailboxes': $!\n";
    @maildirs = map { scalar(s#\n#/new#, $_) } <BOXES>;
}

$/ = "";

for my $dir(@maildirs) {
    opendir DIR, $dir or die "Cannot open directory '$dir': $!\n";
    my @files = grep { !/^\./ && -f "$dir/$_" } readdir(DIR);
    closedir DIR;

    my @emails;
    for(@files) {
        my ($msg, @fields, $from, $to, $date, $epoch, $subject);
        open $msg, "<$dir/$_" or die "Cannot open file '$dir/$_': $!\n";
        @fields = split /\n(?!\s)/, <$msg>;
        close $msg;

        $date = (grep { /^date: /i } @fields)[0] || "jan 1 1970";
        $date =~ s/^date: +//i;
        $epoch = str2time $date; 
        
        # oof, we cant find a real date in the date field.
        # fall back to the time we received the email.
        if(!$epoch || $now - $epoch < 0) {
            $date = (grep { /^Received: / } @fields)[0];
            $date =~ s/^Received:.*?^\t.*?; (\w{3}, +\d{1,2} \w{3} \d{4} \d{2}:\d{2}:\d{2}.*$)/$1/sm;
            $epoch = str2time $date;
        }

        $epoch ||= 0;
        if(($now - $epoch) < 60*60) {
            my $amount = ($now - $epoch) / 60;
            $date = sprintf "%2d min%s ago", $amount, $amount>=2?"s":" ";
        } elsif(($now - $epoch) < 60*60*24) {
            my $amount = ($now - $epoch) / 60 / 60;
            $date = sprintf "%2d hr%s  ago", $amount, $amount>=2?"s":" ";
        } elsif(($now - $epoch) < 60*60*24*7) {
            my $amount = ($now - $epoch) / 60 / 60 / 24;
            $date = sprintf "%2d day%s ago", $amount, $amount>=2?"s":" ";
        } else {
            $date = strftime "%b %d %Y", localtime $epoch;
        }

        $from = (grep { /^from: /i } @fields)[0] || "???";
        $from =~ s/^from: +(?:"?(.+?)"? *<.+>|<?(.+?)>?)$/$+/i;
        $from =~ s/\n//g;
        $from = decode_mimewords($from) if($from =~ /^=\?(ISO-8859-1|koi8-r|utf-8)\?/i);

        $to      = (grep { /^to: /i } @fields)[0];

        $subject = (grep { /^subject: /i } @fields)[0] || "";
        $subject =~ s/^subject: +//i;
        $subject =~ s/\n//g;
        $subject =~ s/\t+/ /g;
        $subject = decode_mimewords($subject) if($subject =~ /^=\?(ISO-8859-1|koi8-r|utf-8)\?/i);
        substr($subject, $width - 43) = "..." if length $subject > $width - 40;

        push @emails, { date => $date, epoch => $epoch, 
            from => $from, to => $to, subject => $subject };
    }

    print "$dir(", scalar @emails,  ")\n" if @emails;
    for(sort { $a->{epoch} <=> $b->{epoch} } @emails) {
        printf "  %-11s  %-23.23s  %-s\n", $_->{date}, $_->{from}, $_->{subject};
    }
}
