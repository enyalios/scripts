#!/usr/bin/perl

use strict;
use warnings;
use LWP::Simple;
use POSIX qw(strftime);
use JSON;
use Socket;
use utf8;
use Time::HiRes qw(time);
binmode(STDOUT, ":utf8");

my $battery_file = "/sys/class/power_supply/BAT0/uevent";
my $brightness_dir = "/sys/class/backlight/backlight";

# icon cheatsheet https://fontawesome.com/v5/cheatsheet
# https://fontawesome.com/icons/categories/weather
my %icon = (
    wifi     => "\x{f1eb}",
    clock    => "\x{f073}",
    load     => "\x{f1fe}",
    traffic  => "\x{f0e8}",
    down     => "\x{25BC}",
    up       => "\x{25B2}",
    plug     => "\x{f1e6}",
    battery0 => "\x{f244}",
    battery1 => "\x{f243}",
    battery2 => "\x{f242}",
    battery3 => "\x{f241}",
    battery4 => "\x{f240}",
    bright   => "\x{f26c}",
    unknown  => "\x{f128}",
    # one icon if the same for day and night
    # an array with "day icon", "night icon" if different
    "Mostly Cloudy"                  => "\x{f0c2}",
    "Mostly Cloudy with Haze"        => "\x{f0c2}",
    "Overcast"                       => "\x{f0c2}",
    "Overcast with Haze"             => "\x{f0c2}",
    "Overcast and Breezy"            => "\x{f72e}\x{f0c2}",
    "Overcast and Windy"             => "\x{f72e}\x{f0c2}",
    "Overcast with Haze and Breezy"  => "\x{f72e}\x{f0c2}",
    "Mostly Cloudy and Breezy"       => "\x{f72e}\x{f0c2}",
    "Mostly Cloudy and Windy"        => "\x{f72e}\x{f0c2}",
    "Light Rain Fog/Mist"            => "\x{f73d}",
    "Light Rain Fog"                 => "\x{f73d}",
    "Light Rain Fog/Mist and Breezy" => "\x{f72e}\x{f73d}",
    "Light Rain Fog/Mist and Windy"  => "\x{f72e}\x{f73d}",
    "Light Rain"                     => "\x{f73d}",
    "Light Rain and Breezy"          => "\x{f72e}\x{f73d}",
    "Light Rain and Windy"           => "\x{f72e}\x{f73d}",
    "Heavy Rain Fog/Mist"            => "\x{f740}",
    "Heavy Rain Fog/Mist and Windy"  => "\x{f72e}\x{f740}",
    "Heavy Rain"                     => "\x{f740}",
    "Rain"                           => "\x{f740}",
    "Rain Fog/Mist"                  => "\x{f740}",
    "Rain Fog/Mist and Breezy"       => "\x{f72e}\x{f740}",
    "Rain Fog/Mist and Windy"        => "\x{f72e}\x{f740}",
    "Rain and Windy"                 => "\x{f72e}\x{f740}",
    "Fog"                            => "\x{f75f}",
    "Fog/Mist"                       => "\x{f75f}",
    "Fog/Mist and Breezy"            => "\x{f72e}\x{f75f}",
    "Sky Obscured with Haze"         => "\x{f75f}",
    "Light Snow"                     => "\x{f2dc}",
    "Light Snow and Breezy"          => "\x{f72e}\x{f2dc}",
    "Light Snow and Windy"           => "\x{f72e}\x{f2dc}",
    "Light Snow Fog/Mist"            => "\x{f2dc}",
    "Light Snow Fog/Mist and Windy"  => "\x{f72e}\x{f2dc}",
    "Snow Fog/Mist"                  => "\x{f2dc}",
    "Snow Fog"                       => "\x{f2dc}",
    "Snow"                           => "\x{f2dc}",
    "Snow and Breezy"                => "\x{f72e}\x{f2dc}",
    "Snow and Windy"                 => "\x{f72e}\x{f2dc}",
    "Blowing Snow and Breezy"        => "\x{f72e}\x{f2dc}",
    "Light Snow Fog/Mist and Breezy" => "\x{f72e}\x{f2dc}",
    "Light Snow Freezing Fog and Breezy"           => "\x{f72e}\x{f2dc}",
    "Thunderstorm"                                 => "\x{f0e7}\x{f740}",
    "Thunderstorm Rain Fog/Mist"                   => "\x{f0e7}\x{f740}",
    "Thunderstorm Rain"                            => "\x{f0e7}\x{f740}",
    "Thunderstorm in Vicinity Heavy Rain Fog/Mist" => "\x{f0e7}\x{f740}",
    "Thunderstorm in Vicinity Heavy Rain"          => "\x{f0e7}\x{f740}",
    "Thunderstorm in Vicinity Rain Fog/Mist"       => "\x{f0e7}\x{f740}",
    "Thunderstorm in Vicinity Rain"                => "\x{f0e7}\x{f740}",
    "Thunderstorm in Vicinity Haze"                => "\x{f0e7}\x{f740}",
    "Thunderstorm in Vicinity Fog/Mist"            => "\x{f0e7}\x{f740}",
    "Thunderstorm in Vicinity Fog and Windy"       => "\x{f72e}\x{f0e7}\x{f740}",
    "Thunderstorm in Vicinity"                     => "\x{f0e7}\x{f740}",
    "Thunderstorm in Vicinity and Windy"           => "\x{f72e}\x{f0e7}\x{f740}",
    "Thunderstorm in Vicinity Light Rain Fog/Mist" => "\x{f0e7}\x{f73d}",
    "Thunderstorm in Vicinity Light Rain"          => "\x{f0e7}\x{f73d}",
    "Thunderstorm in Vicinity Light Rain and Windy" => "\x{f72e}\x{f0e7}\x{f73d}",
    "Thunderstorm Light Rain"                      => "\x{f0e7}\x{f73d}",
    "Thunderstorm Light Rain in Vicinity"          => "\x{f0e7}\x{f73d}",
    "Thunderstorm Light Rain and Windy"            => "\x{f72e}\x{f0e7}\x{f73d}",
    "Thunderstorm Light Rain and Breezy"           => "\x{f72e}\x{f0e7}\x{f73d}",
    "Thunderstorm Heavy Rain"                      => "\x{f0e7}\x{f740}",
    "Thunderstorm Heavy Rain Fog"                  => "\x{f0e7}\x{f740}",
    "Thunderstorm Heavy Rain Fog/Mist"             => "\x{f0e7}\x{f740}",
    "Smoke"                                        => "\x{f75f}",
    "A Few Clouds"               => [ "\x{f6c4}", "\x{f6c3}" ],
    "A Few Clouds with Haze"     => [ "\x{f6c4}", "\x{f6c3}" ],
    "Partly Cloudy"              => [ "\x{f6c4}", "\x{f6c3}" ],
    "Partly Cloudy with Haze"    => [ "\x{f6c4}", "\x{f6c3}" ],
    "Fair"                       => [ "\x{f185}", "\x{f186}" ],
    "Fair with Haze"             => [ "\x{f185}", "\x{f186}" ],
    "A Few Clouds and Breezy"    => [ "\x{f72e}\x{f6c4}", "\x{f72e}\x{f6c3}" ],
    "A Few Clouds and Windy"     => [ "\x{f72e}\x{f6c4}", "\x{f72e}\x{f6c3}" ],
    "Partly Cloudy and Breezy"   => [ "\x{f72e}\x{f6c4}", "\x{f72e}\x{f6c3}" ],
    "Partly Cloudy and Windy"    => [ "\x{f72e}\x{f6c4}", "\x{f72e}\x{f6c3}" ],
    "Fair and Breezy"            => [ "\x{f72e}\x{f185}", "\x{f72e}\x{f186}" ],
    "Fair and Windy"             => [ "\x{f72e}\x{f185}", "\x{f72e}\x{f186}" ],
);

my $SIOCGIFADDR = 0x8915;
my $SIOCGIWESSID = 0x8B1B;
my $benchmark = 0;
$benchmark = 1 if defined $ARGV[0] && $ARGV[0] eq "-benchmark";
my $click_commands = {
    #"date"    => "xterm -title float -e 'date; echo; cal -3; read'",
    "date"    => "firefox -new-tab 'https://calendar.google.com/'; i3-msg workspace 2",
    "weather" => "firefox -new-tab 'https://enyalios.net/cgi-bin/weather.cgi?zip=61874'; i3-msg workspace 2",
    "load"    => "xterm -e top",
    "bright" => {
        1 => "brightness down; killall -HUP i3status",
        3 => "brightness up; killall -HUP i3status",
        4 => "brightness up; killall -HUP i3status",
        5 => "brightness down; killall -HUP i3status",
    },
    #     "weather" => {
    #         1 => "firefox -new-tab 'https://www.wunderground.com/cgi-bin/findweather/getForecast?query=61874'",
    #         3 => "firefox -new-tab 'https://radar.weather.gov/Conus/Loop/centgrtlakes_loop.gif'",
    #     },
};
my $count = 0;
my %traffic;
my $output = "json";
$output = "text" if -t *STDIN;

sub read_file {
    my $retval;
    open my $fh, "<", $_[0] or die "couldnt open $_[0]: $!\n";
    {
        local $/;
        $retval = <$fh>;
    }
    close $fh or die "couldnt close $_[0]: $!\n";
    chomp $retval;
    return $retval;
}

sub write_file {
    open my $fh, ">", $_[0] or die "couldnt open $_[0]: $!\n";
    print $fh $_[1];
    close $fh or die "couldnt close $_[0]: $!\n";
}

sub json {
    return $_[1] unless $output eq "json";
    my $sep_width = 21;
    my $sep = "";
    if($_[0] eq "battery" && $_[1] =~ /Discharging/) {
        $sep_width = 0;
        $sep = ',"separator": false';
        $_[1] .= ", ";
    }
    my $string = "{";
    $string .= '"name": "' . $_[0] . '",';
    $string .= '"full_text": "' . $_[1] . '",';
    if(defined $_[2] && $_[2] ne "") {
        $string .= '"color": "#' . $_[2] . '",'
    }
    $string .= '"separator_block_width": ' . $sep_width;
    $string .= $sep;
    $string .= '}';
    return $string;
}

sub format_time {
    my $sec = $_[0];
    return sprintf "%02d:%02d:%02d", $sec/60/60, int($sec/60)%60, $sec%60;
}

sub create_socket {
    socket(my $socket, PF_INET, SOCK_DGRAM, 0) || return undef;
    return $socket;
}

sub get_ip {
    my ($iface, $socket) = @_;
    my $buf = pack('a256', $iface);
    if(ioctl($socket, $SIOCGIFADDR, $buf) && (my @address = unpack('x20 C4', $buf))) {
        return join('.', @address);
    }
    return undef;
}

sub get_essid {
    my ($iface, $socket) = @_;
    my $ssid_buff = "0"x32;
    my $ssid_info = pack("a16pSS", $iface, $ssid_buff, 32 , 0);
    ioctl($socket, $SIOCGIWESSID, $ssid_info) || return undef;
    my ($if,           # interface name
        $ssid_pointer, # ssid pointer
        $ssid_length,  # ssid length
        $ssid_flags    # ssid flag
    ) = unpack("a16pSS", $ssid_info);
    return undef unless $ssid_length;
    return substr($ssid_pointer, 0, $ssid_length);
}

sub find_wireless_device {
    for(sort glob "/sys/class/net/*/wireless") {
        return $1 if m{^/sys/class/net/(.*)/wireless$};
    }
    return undef;
}

sub get_battery_data {
    my $file = $ENV{BATTERY_UEVENT} // $battery_file;
    return undef unless -e $file;
    my %info;
    for(split "\n",  read_file("$file")) {
        my ($key, $value) = split "=", $_, 2;
        $info{$key} = $value;
    }
    return \%info;
}

sub is_night {
    my (undef,$min,$hour,undef,$mon,undef,undef,undef,undef) = localtime;
    my $time = sprintf "%d%02d", $hour, $min;
    # table made by approximately averaging each month from
    # https://gml.noaa.gov/grad/solcalc/table.php?lat=40&lon=-88&year=2022
    my @sun_table = (
        [708, 1654], # jan
        [646, 1727], # feb
        [634, 1828], # mar
        [615, 1931], # apr
        [539, 2001], # may
        [524, 2022], # jun
        [537, 2018], # jul
        [604, 1947], # aug
        [634, 1859], # sep
        [704, 1812], # oct
        [650, 1646], # nov
        [707, 1630], # dec
    );
    return 1 if $time < $sun_table[$mon][0]; # its before sunrise
    return 1 if $time > $sun_table[$mon][1]; # its after sunset
    return 0;
}

sub weather {
    my (@stat) = stat "$ENV{HOME}/.weather";
    # file is <6 bytes, >50 bytes, or more than 30 minutes old
    if($stat[7] > 50 || $stat[7] < 6 || time - $stat[9] > 30*60) {
        my $zip = read_file("$ENV{HOME}/.zipcode");
        my $weather = get "http://enyalios.net/cgi-bin/weather.cgi?zip=$zip&output=tiny" // "";
        write_file("$ENV{HOME}/.weather", $weather);
    }
    my $string = read_file("$ENV{HOME}/.weather");
    (my $cond = $string) =~ s/ -?\d+$//;
    my $icon = $icon{$cond} // $icon{unknown};
    if(ref $icon{$cond} eq "ARRAY") {
        $icon = $icon{$cond}[is_night()];
    }
    $string = "???" if(length $string > 50 || length $string < 6);
    my $color = "";
    $color = "8888ff" if $string =~ /snow/i;
    return json("weather", "$icon $string", $color);
}

sub load {
    my $string = read_file("/proc/loadavg");
    $string =~ s/ .*$//;
    my $color = "";
    if($string >= 1)      { $color = "ff0000"; }
    elsif($string >= .87) { $color = "ff2a2a"; }
    elsif($string >= .75) { $color = "ff5555"; }
    elsif($string >= .62) { $color = "ff552a"; }
    elsif($string >= .50) { $color = "ff8800"; }
    elsif($string >= .37) { $color = "ffc322"; }
    elsif($string >= .25) { $color = "ffff44"; }
    elsif($string >= .12) { $color = "ffffa1"; }
    return json("load", "$icon{load} $string", $color);
}

sub date {
    return json("date", "$icon{clock} " . strftime("%-m/%-d %T", localtime()));
}

sub battery {
    my $battery = $_[0];
    return unless defined $battery;
    my $status = $battery->{POWER_SUPPLY_STATUS};
    $status = "Full" if $status eq "Not charging";
    my $percent = $battery->{POWER_SUPPLY_CAPACITY};
    $percent //= sprintf "%d", $battery->{POWER_SUPPLY_ENERGY_NOW} / $battery->{POWER_SUPPLY_ENERGY_FULL} * 100;
    my $text = "$status $percent%";
    if($status eq "Discharging") {
        my $charge = $battery->{POWER_SUPPLY_CHARGE_NOW} // $battery->{POWER_SUPPLY_ENERGY_NOW};
        my $current = $battery->{POWER_SUPPLY_CURRENT_NOW} // $battery->{POWER_SUPPLY_POWER_NOW};
        $current *= -1 if $current < 0;
        my $seconds = 0;
        $seconds = $charge/$current*60*60 if $current;
        my $remaining = format_time($seconds);
        $text .= " $remaining remaining";
    }
    my $color = "aa55dd";
    my $icon = "$icon{plug}";
    if($status eq "Discharging") {
        if($percent <= 5 && $count%2) { $color = "000000"; }
        elsif($percent <= 5)  { $color = "ff0000"; }
        elsif($percent <= 10) { $color = "ff0000"; }
        elsif($percent <= 15) { $color = "ff2200"; }
        elsif($percent <= 20) { $color = "ff4400"; }
        elsif($percent <= 25) { $color = "ff6600"; }
        elsif($percent <= 30) { $color = "ff8800"; }
        elsif($percent <= 35) { $color = "ffc322"; }
        elsif($percent <= 40) { $color = "ffff44"; }
        elsif($percent <= 50) { $color = "ddff7f"; }
        elsif($percent <= 60) { $color = "bbffbb"; }
        elsif($percent <= 80) { $color = "ddffdd"; }
        else { $color = ""; }
        if($percent <= 20)    { $icon = "$icon{battery0}"; }
        elsif($percent <= 40) { $icon = "$icon{battery1}"; }
        elsif($percent <= 60) { $icon = "$icon{battery2}"; }
        elsif($percent <= 80) { $icon = "$icon{battery3}"; }
        else                  { $icon = "$icon{battery4}"; }
    }
    return json("battery", "$icon $text", $color);
}

sub watts {
    my $battery = $_[0];
    return unless defined $battery;
    return unless $battery->{POWER_SUPPLY_STATUS} eq "Discharging";
    my $watts;
    if(defined $battery->{POWER_SUPPLY_POWER_NOW}) {
        $watts = $battery->{POWER_SUPPLY_POWER_NOW};
        # power now is in microWatts
        $watts = sprintf "%.2f", $watts/1000000;
    } elsif(defined $battery->{POWER_SUPPLY_CURRENT_NOW}) {
        $watts = $battery->{POWER_SUPPLY_CURRENT_NOW}*$battery->{POWER_SUPPLY_VOLTAGE_NOW};
        # these are in microAmps and microVolts, respectively
        $watts = sprintf "%.2f", $watts/1000000000000;
    } else {
        return;
    }
    $watts *= -1 if $watts < 0;
    return json("watts", "$watts W");
}

sub network {
    my $dev = find_wireless_device();
    return unless defined $dev;
    my $socket = create_socket();
    my $essid = get_essid($dev, $socket);
    return json("network", "$icon{wifi} No wireless networks found", "ff2a2a") unless defined $essid;
    my $ip = get_ip($dev, $socket);
    return json("network", "$icon{wifi} Connecting to $essid", "ff8000") unless defined $ip;
    return json("network", "$icon{wifi} $essid");
}

sub format_traffic {
    my $number = shift;
    my @sizes = split //, "BKMGTPEZY";
    my $index = 0;
    while($number >= 1000 || !$index) {
        $number /= 1024;
        $index++;
        last if $index >= @sizes - 1;
    }
    return sprintf "%.1f %s/s", $number, $sizes[$index];
}

sub traffic {
    my ($down, $up, $current_down, $current_up);
    my $rcstatus = qx"rc-status -a 2>/dev/null | egrep 'net\\.' | egrep -v 'net\\.lo|stopped|inactive'";
    return if $rcstatus eq "";
    for(split "\n", read_file("/proc/net/dev")) {
        next if /^ *lo:/;
        next unless s/^.*: *//;
        my @fields = split " ", $_;
        $down += $fields[0];
        $up += $fields[8]
    }
    my $time = time;
    if(defined $traffic{time} && $traffic{time} != $time) {
        $current_down = ($down - $traffic{down}) / ($time - $traffic{time});
        $current_up   = ($up   - $traffic{up})   / ($time - $traffic{time});
    }
    $traffic{down} = $down;
    $traffic{up}   = $up;
    $traffic{time} = $time;

    return json("traffic", "") unless defined $current_down;
    return json("traffic", sprintf("$icon{traffic} $icon{down} %s, $icon{up} %s", format_traffic($current_down), format_traffic($current_up)));
}

sub bright {
    my $dir = $ENV{BRIGHTNESS_DIR} // $brightness_dir;
    return unless -d $dir;
    my $brightness = read_file("$dir/brightness");
    my $max_brightness = read_file("$dir/max_brightness");
    return json("bright", sprintf "$icon{bright} %d", $brightness/$max_brightness*100);
}

sub cpu_temp {
    my $temp = read_file("/sys/class/thermal/thermal_zone1/temp");
    $temp = sprintf "%.1f", $temp / 1000;
    # todo - add color
    return json("cpu_temp", $temp);
}


$| = 1;
print "{\"version\":1,\"click_events\":true}\n[\n" if $output eq "json";
my $pid = fork();
die "couldnt fork\n" unless defined $pid;
if($pid) {
    # parent
    close STDIN;
    my $sep = ($output eq "json")?",":" | ";
    while(++$count) {
        eval {
            $SIG{HUP} = sub { die; };
            print "[" if $output eq "json";
            my $battery = get_battery_data();
            my $string = join($sep, traffic(), network(), weather(), battery($battery), watts($battery), date());
            print $string unless $benchmark;
            print "]," if $output eq "json";
            print "\n" unless $benchmark;
            sleep 1 unless $benchmark;
            exit if $benchmark && $count >= 10000;
        };
    }
} else {
    # child
    $SIG{HUP} = 'IGNORE';
    close STDOUT;
    open my $log, ">$ENV{HOME}/.i3/i3status.log" or die "couldnt open log: $!\n";
    # autoflush the $log filehandle
    select((select($log), $|=1)[0]);
    while(my $line = <STDIN>) {
        if($line =~ /^,?{/) {
            $line =~ s/^,//;
            my $tree = decode_json $line;
            my ($button, $name) = ($tree->{button}, $tree->{name});
            print $log "click event: name=$name, button=$button\n";
            my $command = $click_commands->{$name};
            $command = $command->{$button} if ref $command eq "HASH";
            if(defined $command) {
                print $log "executing command \"$command\"\n";
                system("$command &");
            }
        }
    }
}
